group 'codeOrchestra.colt'
version '1.3.1'
buildscript {
    repositories {
        mavenCentral()
        mavenLocal()
    }
    dependencies { classpath 'org.ajoberstar:gradle-git:1.2.0' }
}

allprojects {
    apply plugin: 'groovy'
    sourceSets {
        main {
            java { srcDirs = [] }
            groovy { srcDirs += ["src/main/java"] }
            output.classesDir = "$rootProject.projectDir.path/out/production/$project.name"
            output.resourcesDir = "$rootProject.projectDir.path/out/production/$project.name"
        }
        test {
            java { srcDirs = [] }
            groovy { srcDirs += ["src/test/java"] }
            output.classesDir = "$rootProject.projectDir.path/out/test/$project.name"
            output.resourcesDir = "$rootProject.projectDir.path/out/test/$project.name"
        }
    }
    compileJava {
        options.fork = true
        options.forkOptions.executable = "$JDK_HOME/bin/javac"
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }
    repositories {
        mavenCentral()
        mavenLocal()
    }
    dependencies {
        compile 'org.codehaus.groovy:groovy-all:2.4.4'
        compile ('com.googlecode.apparat:apparat-core:1.0-RC9') { transitive = false }
        compile ('com.googlecode.apparat:apparat-log:1.0-RC9') { transitive = false }
        compile 'org.apache.ant:ant:1.9.2'
        compile 'org.antlr:antlr4:4.5'
        compile 'com.aquafx-project:aquafx:0.1'
        compile 'commons-codec:commons-codec:1.3'
        compile 'org.controlsfx:controlsfx:8.0.2'
        compile 'commons-httpclient:commons-httpclient:3.0.1'
        compile 'org.mortbay.jetty:jetty:6.1.26'
        compile 'org.mortbay.jetty:jetty-util:6.1.26'
        compile 'com.fasterxml.jackson.core:jackson-annotations:2.2.0'
        compile 'com.fasterxml.jackson.core:jackson-core:2.2.0'
        compile 'com.fasterxml.jackson.core:jackson-databind:2.2.0'
        compile 'org.apache.logging.log4j:log4j-api:2.0-beta8'
        compile 'org.apache.logging.log4j:log4j-core:2.0-beta8'
        compile 'net.java.openjfx.backport:openjfx-78-backport:1.8.0-ea-b96.1'
        compile 'javax.portlet:portlet-api:2.0'
        compile 'javax.servlet:servlet-api:2.4'
        compile 'org.slf4j:slf4j-api:1.7.5'
        compile 'org.slf4j:slf4j-simple:1.7.5'
        compile 'net.lingala.zip4j:zip4j:1.3.2'
        compile 'javax.jmdns:jmdns:3.4.1'
        compile 'org.ow2.asm:asm:4.0'
        compile 'org.ow2.asm:asm-analysis:4.0'
        compile 'org.ow2.asm:asm-commons:4.0'
        compile 'org.ow2.asm:asm-tree:4.0'
        compile 'org.ow2.asm:asm-util:4.0'
        compile 'org.scala-lang:scala-library:2.8.0'
    }
}

project('colt-updater') {
    apply plugin: 'java'
    compileGroovy.enabled = false;
    sourceSets {
        main {
            java { srcDirs = ["src/main/java"] }
            groovy { srcDirs = [] }
            output.classesDir = "$rootProject.projectDir.path/out/production/$project.name"
            output.resourcesDir = "$rootProject.projectDir.path/out/production/$project.name"
        }
    }
}
project('colt-as') {
    dependencies {
        compile fileTree(dir: project(":colt-core").projectDir.path + '/lib', include: '*.jar')
        compile fileTree(dir: 'lib', include: '*.jar')
        compile project(':colt-core')
    }
}
project('colt-core') {
    dependencies {
        compile fileTree(dir: 'lib', include: '*.jar')
    }
}

[compileGroovy, compileJava]*.enabled = false
sourceSets {
    main { [groovy, java, resources]*.srcDirs = [] }
    test { [groovy, java, resources]*.srcDirs = [] }
}

task cloneModules << {
    def dir = project(":colt-as").projectDir
    if(!dir.exists() || !file(dir.path + "/.git").exists()) {
        dir.deleteDir()
        org.ajoberstar.grgit.Grgit.clone(dir: dir, uri: COLT_AS_GIT)
    }
    dir = project(":colt-core").projectDir
    if(!dir.exists() || !file(dir.path + "/.git").exists()) {
        dir.deleteDir()
        org.ajoberstar.grgit.Grgit.clone(dir: dir, uri: COLT_CORE_GIT)
    }
}

task preBuild(dependsOn: 'cloneModules', type: Copy) {
    def coltasPath = project(":colt-as").projectDir.path
    def out = "out/bin/win"
    ["flex_sdk", "projects"].findResults { out + "/" + it }.each { if (!file(it).exists()) mkdir(it) }
    file(out + "/lib").deleteDir()
    copy {
        from coltasPath + "/lib/colt.swc"
        into out + "/lib"
    }
    file(out + "/templates").deleteDir()
    copy {
        from coltasPath + "/templates"
        into out + "/templates"
        exclude "*.sh"
    }
    copy {
        from coltasPath + "/crossdomain.xml"
        into out
    }
    copy {
        from files("src/deploy/package/colticon.ico", "src/deploy/package/coltAS.properties")
        into out
    }
}
task postBuild(dependsOn: 'build', type: Copy) {
    def lib = "out/bin/win/lib"
    allprojects.each {project ->
        ["build/libs", "lib"].each {dir ->
            copy {
                from project.projectDir.path + "/" + dir
                into lib
                include "*.jar"
            }
        }
    }
    copy {
        from configurations.compile
        into lib
    }
}
clean.doFirst {
    delete "$rootProject.projectDir.path/out/production"
}
compileJava.dependsOn preBuild

apply plugin: 'application'
mainClassName = 'codeOrchestra.colt.core.ui.ColtApplication'
dependencies {
    runtime project(":colt-updater")
    runtime project(":colt-as")
}
task run(dependsOn: 'postBuild', type: JavaExec, overwrite: true) {
    main = "$mainClassName"
    classpath = files("$rootProject.projectDir.path/out/bin/win/lib").asFileTree
    workingDir = file("$rootProject.projectDir.path/out/bin/win")
}